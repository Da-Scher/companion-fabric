plugins {
	id 'fabric-loom' version '0.2.5-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}

repositories {
	mavenCentral()
}

configurations {
	// configuration that holds jars to include in the jar
	extraLibs
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
	extraLibs 'com.moandjiezana.toml:toml4j:0.7.2'

	// Fabric API
	modCompile "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Testing engines
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.1'

	// Downloads the jars
	configurations.compile.extendsFrom(configurations.extraLibs)
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from {
		configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
	}
	from "LICENSE"
}

test {
	useJUnitPlatform()
	maxHeapSize = "1G"
	reports.html.enabled = false

	filter {
		includeTestsMatching "io.minepkg.companion.test.*"
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(jar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
	}
}
